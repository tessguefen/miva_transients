<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tg_transients">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Transients">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.011">
	<MvCOMMENT>
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.012">
	</MvCOMMENT>
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Save Cached versions of data, set an expiration time, and be happy.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "util, vis_util, data_store, json, clientside, scheduledtask, provision_store">
	<MvCOMMENT>
	<MvASSIGN NAME = "l.module:features"	VALUE = "util, vis_util, data_store, json, clientside, scheduledtask, provision_store, json_api">
	</MvCOMMENT>
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	INSTALL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvCOMMENT>
	===========================
	Create Transients Table
	===========================
	</MvCOMMENT>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE TABLE ' $  g.Store_Table_Prefix $ 'Transients
						  (
							tkey		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
							expires	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ ',
							tvalue	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
						  ) ' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-INSTALL-1001:', 'An error occured while creating the table Transients. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'Transients ON ' $ g.Store_Table_Prefix $ 'Transients ( tkey )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'Transients' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-INSTALL-1002:', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'Transients' }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	VIS_UTIL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "StoreUtilityModule_Action" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_LeftNavigation" PARAMETERS = "module var, ident" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Screen" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "StoreUtilityModule_Validate" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	VIS_UTIL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Utility_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TRANSIENTS' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="AllTransients"></div>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab EQ 'TRANSIENTS' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AllTransients_functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AllTransients.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new AllTransients_Batchlist(); } );
		</script>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Tabs" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = "TRANSIENTS:Transients" />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Update" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Utility_Validate" PARAMETERS = "module_var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	CLIENTSIDE
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ g.Filename EQ 'AllTransients_functions.js' }">
		<MvINCLUDE FILE = "js/AllTransients_functions.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'AllTransients.js' }">
		<MvINCLUDE FILE = "js/AllTransients.js">
	</MvIF>
</MvFUNCTION>


<MvCOMMENT>
| ========================================================================================================================
|	JSON
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">
		<MvFUNCTIONRETURN>
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AllTransients_Load_Query' }">
		<MvEVAL EXPR = "{ Batchlist_Transients( l.module) }">
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Delete_Transient' }">
		<MvIF EXPR = "{ Delete_Transient( trim( g.Transient_Key ) ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'tkey', 'An Error Occurred.' ) }">
		</MvIF>
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Delete_All_Transients' }">
		<MvIF EXPR = "{ Delete_All_Transients() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-JSON-0001', 'An Error Occurred.' ) }">
		</MvIF>
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Delete_Expired_Transients' }">
		<MvIF EXPR = "{ Delete_Expired_Transients() }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
		<MvELSE>
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-JSON-0002', 'An Error Occurred.' ) }">
		</MvIF>
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	PROVISION_STORE
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Provision_Store" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.child_xml" ARRAY = "l.provide_xml:children">
		<MvASSIGN NAME = "l.name"		VALUE = "{ tolower( l.child_xml:name ) }">

		<MvIF EXPR = "{ l.name EQ 'delete_transient' }">
			<MvEVAL EXPR = "{ Module_Provision_Delete_Transient( l.module, l.child_xml ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'delete_all_transients'}">
			<MvEVAL EXPR = "{ Module_Provision_Delete_All_Transients( l.module ) }">
		<MvELSEIF EXPR = "{ l.name EQ 'delete_expired_transients'}">
			<MvEVAL EXPR = "{ Module_Provision_Delete_Expired_Transients( l.module ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	JSON_API
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON_API" PARAMETERS = "module var, function" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ miva_output_header( 'Content-Type', 'application/json' ) }" >
	<MvIF EXPR = "{ l.function EQ 'Delete_Transient' }">				<MvFUNCTIONRETURN VALUE = "{ JSON_API_Delete_Transient( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'Delete_All_Transients' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_Delete_All_Transients( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'Delete_Expired_Transients' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_API_Delete_Expired_Transients( l.module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-ERROR-0001', 'Invalid function.' ) }">
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	MODULE FUNCTIONS
|	1.	Get_Transient( key )
|	2.	Set_Transient( key, value, expires )
|	3.	Transient_ReadyTheme_NavigationSet( readytheme_code, expires )
|	4.	Transient_ReadyTheme_NavigationSet_WithSettings( readytheme_code, expires, all_settings var )
|	5.	Transient_Load_NavigationSet( readytheme_code )
|	6.	Transient_ReadyTheme_Image( readytheme_code, expires )
|	7.	Transient_Load_Link( item var )
|	8.	Transient_ReadyTheme_ContentSection( readytheme_code, expires, all_settings var )
|	9.	Transient_ReadyTheme_ContentSection_Lowlevel( key, readytheme_code, expires, all_settings var )
|	10.	Delete_Transient( key )
|	11. Batchlist_Transients( module var)
|	12. Delete_All_Transients()
|	13. Delete_Expired_Transients()
|	14.	Module_Provision_Delete_Transient( module var, provide_xml var )
|	15.	Module_Provision_Delete_All_Transients( module var )
|	16.	Module_Provision_Delete_Expired_Transients( module var )
|	17.	JSON_API_Delete_Transient( module var )
|	18.	JSON_API_Delete_All_Transients( module var )
|	19. JSON_API_Delete_Expired_Transients( module var )
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Get_Transient" PARAMETERS = "key" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.key" VALUE = "{ trim( l.key ) }">

	<MvIF EXPR = "{ ISNULL l.key }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvIF EXPR = "{ NOT Get_Transient_LowLevel( l.key, l.output ) }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.output }">
</MvFUNCTION>

<MvFUNCTION NAME = "Get_Transient_LowLevel" PARAMETERS = "key, output var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
					VIEW 	= "GetTransients"
					QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'Transients WHERE tkey = ?' }"
					FIELDS	= "l.key">

	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GetTransients">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-MOD-2000:', 'An error occurred while retrieving the requestest Transient.' ) }">
	</MvIF>

	<MvIF EXPR = "{ GetTransients.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "GetTransients">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( '#Error# Could Not Find KEY' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.transient:key"		VALUE = "{ GetTransients.d.tkey }">
	<MvASSIGN NAME = "l.transient:expires"	VALUE = "{ GetTransients.d.expires }">
	<MvASSIGN NAME = "l.transient:value"	VALUE = "{ GetTransients.d.tvalue }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "GetTransients">

	<MvCOMMENT> === [ Check if Transient is expired ] === </MvCOMMENT>
	<MvIF EXPR = "{ s.dyn_time_t GT l.transient:expires }">
		<MvASSIGN NAME = "l.success" VALUE = "{ Delete_Transient( l.transient:key ) }">
	</MvIF>

	<MvASSIGN NAME = "l.output" VALUE = "{ l.transient:value }">

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Set_Transient" PARAMETERS = "key, value, expires" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.value" VALUE = "{ trim( l.value ) }">
	<MvASSIGN NAME = "l.key" VALUE = "{ trim( l.key ) }">
	<MvASSIGN NAME = "l.expires" VALUE = "{ trim( l.expires ) }">
	
	<MvASSIGN NAME = "l.transient" VALUE = "{ Get_Transient( l.key ) }">

	<MvIF EXPR = "{ NOT l.value OR NOT l.key OR NOT l.expires }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ l.transient }">
		<MvASSIGN NAME = "l.success" VALUE = "{  Delete_Transient( l.key ) }">
	</MvIF>

	<MvASSIGN NAME = "l.expires" VALUE = "{ l.expires + s.dyn_time_t }">

	<MvQUERY	NAME 	= "Merchant"
				QUERY 	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'Transients
								( tkey, tvalue, expires )
								VALUES
								(?, ?, ?)'}"
			 	FIELDS = "l.key, l.value, l.expires">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-MOD-2001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_ReadyTheme_NavigationSet" PARAMETERS = "readytheme_code, expires" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.expires"			VALUE = "{ trim( l.expires ) }">
	<MvASSIGN NAME = "l.readytheme_code"	VALUE = "{ trim( l.readytheme_code ) }">
	<MvASSIGN NAME = "l.key" VALUE = "{ 'navigationset__' $ l.readytheme_code }">

	<MvIF EXPR = "{ NOT l.expires OR NOT l.readytheme_code }">
		<MvFUNCTIONRETURN>
	</MvIF>
	
	<MvASSIGN NAME = "l.get_transient" VALUE = "{ Get_Transient( l.key ) }">
	<MvIF EXPR = "{ l.get_transient }">
		<MvFUNCTIONRETURN VALUE = "{ l.get_transient }">
	</MvIF>

	<MvASSIGN NAME = "l.navigationset" VALUE = "{ Transient_Load_NavigationSet( l.readytheme_code ) }">

	<MvIF EXPR = "{ NOT l.navigationset }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.navigationset:templ_id, l.navigationset_template ) }">
		<MvASSIGN NAME = "l.readytheme_prev" VALUE = "{ l.all_settings:readytheme }">
		<MvASSIGN NAME = "l.all_settings:readytheme" VALUE = "{ l.navigationset }">

		<MvCAPTURE VARIABLE = "l.return"><MvEVAL EXPR = "{ [ g.Store_Template_Path $ l.navigationset_template:filename ].Template_Render( l.settings, l.all_settings ) }"></MvCAPTURE>
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:readytheme" VALUE = "{ l.readytheme_prev }">

	<MvIF EXPR = "{ Set_Transient( l.key, l.return, l.expires )  }">
		<MvFUNCTIONRETURN VALUE = "{ l.return }">
	</MvIF>

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_ReadyTheme_NavigationSet_WithSettings" PARAMETERS = "readytheme_code, expires, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.expires"			VALUE = "{ trim( l.expires ) }">
	<MvASSIGN NAME = "l.readytheme_code"	VALUE = "{ trim( l.readytheme_code ) }">
	<MvASSIGN NAME = "l.key" VALUE = "{ 'navigationset__' $ l.readytheme_code }">

	<MvIF EXPR = "{ NOT l.expires OR NOT l.readytheme_code }">
		<MvFUNCTIONRETURN>
	</MvIF>
	
	<MvASSIGN NAME = "l.get_transient" VALUE = "{ Get_Transient( l.key ) }">
	<MvIF EXPR = "{ l.get_transient }">
		<MvFUNCTIONRETURN VALUE = "{ l.get_transient }">
	</MvIF>

	<MvASSIGN NAME = "l.navigationset" VALUE = "{ Transient_Load_NavigationSet( l.readytheme_code ) }">

	<MvIF EXPR = "{ NOT l.navigationset }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.navigationset:templ_id, l.navigationset_template ) }">
		<MvASSIGN NAME = "l.readytheme_prev" VALUE = "{ l.all_settings:readytheme }">
		<MvASSIGN NAME = "l.all_settings:readytheme" VALUE = "{ l.navigationset }">

		<MvCAPTURE VARIABLE = "l.return"><MvEVAL EXPR = "{ [ g.Store_Template_Path $ l.navigationset_template:filename ].Template_Render( l.null, l.all_settings ) }"></MvCAPTURE>
	</MvIF>

	<MvASSIGN NAME = "l.all_settings:readytheme" VALUE = "{ l.readytheme_prev }">

	<MvIF EXPR = "{ Set_Transient( l.key, l.return, l.expires )  }">
		<MvFUNCTIONRETURN VALUE = "{ l.return }">
	</MvIF>

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_Load_NavigationSet" PARAMETERS = "readytheme_code" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL ="">
	<MvASSIGN NAME = "l.readytheme_code" VALUE = "{ trim( l.readytheme_code ) }">

	<MvIF EXPR = "{ NOT l.readytheme_code }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Root $ 'modules/component/readytheme.mvc' ].ReadyTheme_NavigationSet_Load_Code( l.readytheme_code, l.navigationset ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.navigationitem_count" VALUE = "{ [ g.Module_Root $ 'modules/component/readytheme.mvc' ].ReadyTheme_NavigationItemList_Load_Heirarchy( l.navigationset:id, l.navigationset:navigationitems ) }">

	<MvIF EXPR = "{ NOT l.navigationitem_count }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.temp" VALUE = "{ miva_array_filter_ref( l.navigationset:navigationitems, 0, l.nav_item, 'l.nav_item:active EQ 1', l.navigationset_new:navigationitems ) }" />

	<MvASSIGN NAME = "l.navigationset:navigationitems" VALUE = "{ l.navigationset_new:navigationitems }" />

	<MvFOREACH ITERATOR = "l.navigationitem" ARRAY = "l.navigationset:navigationitems">
		<MvIF EXPR = "{ l.navigationitem:active EQ 0 }">
			<MvFOREACHCONTINUE />
		</MvIF>

		<MvASSIGN NAME = "l.navigationitem" VALUE = "{ Transient_Load_Link( l.navigationitem ) }">
		<MvIF EXPR = "{ l.navigationitem:items }">
			<MvASSIGN NAME = "l.temp" VALUE = "{ miva_array_filter_ref( l.navigationitem:items, 0, l.nav_item2, 'l.nav_item2:active EQ 1', l.navigationitem_new:items ) }" />

			<MvASSIGN NAME = "l.navigationitem:items" VALUE = "{ l.navigationitem_new:items }" />

			<MvFOREACH ITERATOR = "l.navigationitem_level2" ARRAY = "l.navigationitem:items">
				<MvIF EXPR = "{ l.navigationitem_level2:active EQ 0 }">
					<MvFOREACHCONTINUE />
				</MvIF>

				<MvASSIGN NAME = "l.navigationitem_level2" VALUE = "{ Transient_Load_Link( l.navigationitem_level2 ) }">
				<MvIF EXPR = "{ l.navigationitem_level2:items }">
					<MvASSIGN NAME = "l.temp" VALUE = "{ miva_array_filter_ref( l.navigationitem_level2:items, 0, l.nav_item3, 'l.nav_item3:active EQ 1', l.navigationitem_level2_new:items ) }" />

					<MvASSIGN NAME = "l.navigationitem_level2:items" VALUE = "{ l.navigationitem_level2_new:items }" />

					<MvFOREACH ITERATOR = "l.navigationitem_level3" ARRAY = "l.navigationitem_level2:items">
						<MvIF EXPR = "{ l.navigationitem_level3:active EQ 0 }">
							<MvFOREACHCONTINUE>
						</MvIF>

						<MvASSIGN NAME = "l.navigationitem_level3" VALUE = "{ Transient_Load_Link( l.navigationitem_level3 ) }">
					</MvFOREACH>
				</MvIF>
			</MvFOREACH>
		</MvIF>
	</MvFOREACH>
	<MvFUNCTIONRETURN VALUE = "{ l.navigationset }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_ReadyTheme_Image" PARAMETERS = "readytheme_code, expires" STANDARDOUTPUTLEVEL = "compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.readytheme_code" VALUE = "{ trim( l.readytheme_code ) }">
	<MvASSIGN NAME = "l.expires" VALUE = "{ trim( l.expires ) }">
	<MvASSIGN NAME = "l.key" VALUE = "{ 'image__' $ l.readytheme_code }">

	<MvIF EXPR = "{ NOT l.readytheme_code OR NOT l.expires }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.get_transient" VALUE = "{ Get_Transient( l.key ) }">
	<MvIF EXPR = "{ l.get_transient }">
		<MvFUNCTIONRETURN VALUE = "{ l.get_transient }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Root $ 'modules/component/readytheme.mvc' ].ReadyTheme_Image_Load_Code( l.readytheme_code, l.image ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Image_Load_ID( l.image:image_id, l.loaded_image ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.image:url" VALUE = "{ l.loaded_image:image }">

	<MvIF EXPR = "{ l.image:link_type NE 'N'}">
		<MvASSIGN NAME = "l.image" VALUE = "{ Transient_Load_Link( l.image ) }">
	</MvIF>

	<MvIF EXPR = "{ l.image:image_wdth AND l.image:image_hght }">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Image_Load_File( l.image:url, l.crop_image ) }">
			<MvIF EXPR = "{ [ g.Module_Library_DB ].GeneratedImage_FindOrInsert_Image_Dimensions( l.crop_image, l.image:image_wdth, l.image:image_hght, l.generated_image ) }">
				<MvASSIGN NAME = "l.image:cropped_url" VALUE = "{ l.generated_image:image }">
			</MvIF>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.return" VALUE = "{ miva_array_serialize( l.image ) }">
	<MvIF EXPR = "{ Set_Transient( l.key, l.return, l.expires )  }">
		<MvFUNCTIONRETURN VALUE = "{ l.return }">
	</MvIF>

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_Load_Link" PARAMETERS = "item var" STANDARDOUTPUTLEVEL = "compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.item }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.flags:nosession" VALUE = "1" />

	<MvIF EXPR = "{ l.item:link_type EQ 'P' AND l.item:link_dest }">
		<MvDO FILE = "{ g.Module_Feature_URI_UT }" NAME = "l.item_link" VALUE = "{ Store_Product_Code_URL(  l.item:link_dest, l.flags ) }">
		<MVIF EXPR = "{ l.item_link }">
			<MvASSIGN NAME = "l.item:link" VALUE = "{ '<a href="' $ l.item_link $ '" target="' $ l.item:link_targ $ '">' $ l.item:name $ '</a>' }">
			<MvASSIGN NAME = "l.item:link_url" VALUE = "{ l.item_link }">
			<MvFUNCTIONRETURN VALUE = "{ l.item }" />
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.item:link_type EQ 'C' AND l.item:link_dest }">
		<MvDO FILE = "{ g.Module_Feature_URI_UT }" NAME = "l.item_link" VALUE = "{ Store_Category_Code_URL(  l.item:link_dest, l.flags ) }">
		<MVIF EXPR = "{ l.item_link }">
			<MvASSIGN NAME = "l.item:link" VALUE = "{ '<a href="' $ l.item_link $ '" target="' $ l.item:link_targ $ '">' $ l.item:name $ '</a>' }">
			<MvASSIGN NAME = "l.item:link_url" VALUE = "{ l.item_link }">
			<MvFUNCTIONRETURN VALUE = "{ l.item }" />
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.item:link_type EQ 'G' AND l.item:link_dest }">
		<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.item:link_dest, l.item:loaded_page ) }">
			<MvIF EXPR = "{ l.item:loaded_page:id }">
				<MvDO FILE = "{ g.Module_Feature_URI_UT }" NAME = "l.item_link" VALUE = "{ Store_Page_URL(  l.item:loaded_page, l.flags ) }">
				<MvIF EXPR = "{ l.item_link }">
					<MvASSIGN NAME = "l.item:link" VALUE = "{ '<a href="' $ l.item_link $ '" target="' $ l.item:link_targ $ '">' $ l.item:name $ '</a>' }">
					<MvASSIGN NAME = "l.item:link_url" VALUE = "{ l.item_link }">
					<MvFUNCTIONRETURN VALUE = "{ l.item }" />
				</MvIF>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.item:link_type EQ 'U' AND l.item:link_dest }">
		<MvASSIGN NAME = "l.item:link" VALUE = "{ '<a href="' $ l.item:link_dest $ '" target="' $ l.item:link_targ $ '">' $ l.item:name $ '</a>' }">
		<MvASSIGN NAME = "l.item:link_url" VALUE = "{ l.link_dest }">
		<MvFUNCTIONRETURN VALUE = "{ l.item }" />
	</MvIF>

	<MvASSIGN NAME = "l.item:link" VALUE = "{ l.item:name }">
	<MvFUNCTIONRETURN VALUE = "{ l.item }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_ReadyTheme_ContentSection" PARAMETERS = "readytheme_code, expires, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.expires"			VALUE = "{ trim( l.expires ) }">
	<MvASSIGN NAME = "l.readytheme_code"	VALUE = "{ trim( l.readytheme_code ) }">
	<MvASSIGN NAME = "l.key" VALUE = "{ 'contentsection__' $ l.readytheme_code }">

	<MvFUNCTIONRETURN VALUE = "{ Transient_ReadyTheme_ContentSection_Lowlevel( l.key, l.readytheme_code, l.expires, l.all_settings) }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Transient_ReadyTheme_ContentSection_Lowlevel" PARAMETERS = "key, readytheme_code, expires, all_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.expires OR NOT l.readytheme_code }">
		<MvFUNCTIONRETURN>
	</MvIF>
	
	<MvASSIGN NAME = "l.get_transient" VALUE = "{ Get_Transient( l.key ) }">
	<MvIF EXPR = "{ l.get_transient }">
		<MvFUNCTIONRETURN VALUE = "{ l.get_transient }">
	</MvIF>

	<MvDO FILE = "{ g.Module_Root $ 'modules/component/readytheme.mvc' }" NAME = "l.load_content_section" VALUE = "{ ReadyTheme_ContentSection_Load_Code( l.readytheme_code, l.loaded_content_section ) }">

	<MvIF EXPR = "{ NOT l.loaded_content_section OR l.loaded_content_section:active EQ 0 }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.loaded_content_section:templ_id, l.content_section ) }">
		<MvCAPTURE VARIABLE = "l.return"><MvEVAL EXPR = "{ [ g.Store_Template_Path $ l.content_section:filename ].Template_Render( l.null, l.all_settings ) }"></MvCAPTURE>
	</MvIF>

	<MvIF EXPR = "{ Set_Transient( l.key, l.return, l.expires )  }">
		<MvFUNCTIONRETURN VALUE = "{ l.return }">
	</MvIF>

	<MvFUNCTIONRETURN>
</MvFUNCTION>

<MvFUNCTION NAME = "Delete_Transient" PARAMETERS = "key" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME 	= "Merchant"
				QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Transients WHERE tkey = ?' }"
			 	FIELDS = "l.key">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Batchlist_Transients" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvASSIGN NAME = "g.Filter" VALUE = "{ trim( g.Filter ) }">
	<MvASSIGN NAME = "g.Sort" VALUE = "{ trim( g.Sort ) }">
	<MvASSIGN NAME = "g.Offset" VALUE = "{ trim( g.Offset ) }">
	<MvASSIGN NAME = "g.Count" VALUE = "{ trim( g.Count ) }">
	<MvASSIGN NAME = "l.search_query" VALUE = "">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'Transients', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, g.Filter,'tkey:s.tkey,tvalue:s.tvalue,expires:s.expires' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields(l.search_query, g.Sort, 'tkey:s.tkey,tvalue:s.tvalue,expires:s.expires', 's.tkey' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error(g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range('Merchant', 'Transients', l.search_sql, l.search_fields, g.Offset, g.Count) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-MOD-2005',g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
		{
			"data":
				[
					<MvWHILE EXPR = "{ ( NOT Transients.d.EOF ) AND ( ( g.Count EQ 0 ) OR (l.count LT g.Count ) ) }">
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.count )}">
					"tkey": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( Transients.d.tkey ) }">",
					"tvalue": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( Transients.d.tvalue ) }">",
					"expires": "<MvEVAL EXPR = "{ int( Transients.d.expires ) }">",
					<MvASSIGN NAME = "l.fdt" VALUE = "{ [ g.Module_Library_Utilities].Format_Date( Transients.d.expires, s.miva_language ) }">
					<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.fdt" VALUE = "{ l.fdt $ ' ' $ Format_Time( Transients.d.expires, s.miva_language ) }">
					"f_expires": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.fdt ) }">"
					<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">
					<MvSKIP NAME = "Merchant" VIEW = "Transients" ROWS = 1>
					</MvWHILE>
				],

			"total_count": <MvEVAL EXPR = "{ int( l.total_count ) }">,
			"start_offset": <MvEVAL EXPR = "{ int( g.Offset ) }">
		}
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Transients">
	}
</MvFUNCTION>

<MvFUNCTION NAME = "Delete_All_Transients" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME 	= "Merchant"
				QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Transients'}">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-MOD-2002', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Delete_Expired_Transients" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY	NAME 	= "Merchant"
				QUERY 	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'Transients WHERE expires < ?'}"
				FIELDS	= "s.dyn_time_t">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-MOD-2003', g.MvQUERY_Error ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Delete_Transient" PARAMETERS = "module var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Delete_Transient key="my_key_here" />
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_PRV_AD ].PRV_Attribute_Text( l.provide_xml, 'key', l.key ) }">
		<MvFUNCTIONRETURN />
	</MvIF>

	<MvIF EXPR = "{ NOT Delete_Transient( l.key ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Error - Transient Not Deleted.' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Delete_All_Transients" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Delete_All_Transients />
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT Delete_All_Transients() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Error - Transients Not Deleted.' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Provision_Delete_Expired_Transients" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		<Delete_Expired_Transients />
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT Delete_Expired_Transients() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Feature_PRV_AD ].PRV_LogMessage( l.provide_xml, 'Error - Expired Transients Not Deleted.' ) }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Delete_Transient" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		--- How do you access the request
		<MvIF EXPR = "{ Delete_Transient( trim( [[ KEY HERE ]] ) ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
		</MvIF>
	</MvCOMMENT>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-JSON-ERROR-0002', 'Transient not deleted.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Delete_All_Transients" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ Delete_All_Transients() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-JSON-ERROR-0003', 'Transients not deleted.' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Delete_Expired_Transients" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ Delete_Expired_Transients() }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'TRANSIENTS-JSON-ERROR-0004', 'Expired Transients not deleted.' ) }">
</MvFUNCTION>


<MvCOMMENT>
=== SCHEDULED TASK PARAMETERS
</MvCOMMENT>

<MvFUNCTION NAME = "ScheduledTaskModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings" VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision" VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Operations" PARAMETERS = "module var, operations var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "code"		VALUE = "all">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "descrip"	VALUE = "Delete All Transients">
	<MvASSIGN NAME = "l.operations" INDEX = "2" MEMBER = "code"		VALUE = "expired">
	<MvASSIGN NAME = "l.operations" INDEX = "2" MEMBER = "descrip"	VALUE = "Delete Expired Transients">

	<MvFUNCTIONRETURN VALUE = 2>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Fields" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Validate" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Update" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Delete" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Execute" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.task:operation EQ 'all' }">
		<MvFUNCTIONRETURN VALUE = "{ Delete_All_Transients() }">
	</MvIF>

	<MvIF EXPR = "{ l.task:operation EQ 'expired' }">
		<MvFUNCTIONRETURN VALUE = "{ Delete_Expired_Transients() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TRANSIENTS-MOD-3000', 'Unsupported operation' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Provision_Settings" PARAMETERS = "module var, task var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>